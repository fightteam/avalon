apply plugin: "base"

buildscript {
    // 仓库配置
 	repositories {
        mavenLocal()
        mavenCentral()

	}
	dependencies {

    }
}
// 一些简单的配置参数
// -----------------------------
// jar包版本依赖在gradle.properties中管理
ext {
    gradleScriptDir = "${rootProject.projectDir}/gradle"
}
// 所有项目共同的配置
// -----------------------
// 项目编译参数设置、依赖等等
configure(allprojects) {
    apply plugin: "java"
    apply plugin: "idea"
    apply plugin: "eclipse"
    apply from: "${gradleScriptDir}/ide.gradle"

    group "org.fightteam.avlon"

    configurations.all {
        // 不缓存模块的改动
        resolutionStrategy.cacheChangingModulesFor 0, 'seconds'

        // 使所有的spring framework 库依赖版本统一
        resolutionStrategy.eachDependency { DependencyResolveDetails details ->
            if (details.requested.group == 'org.springframework') {
                details.useVersion "$springVersion"
            }
        }
        // 排除spring库中使用的common log api
        exclude group: "commons-logging"
        exclude module: "slf4j-log4j12"
    }

    // 项目构建JDK版本
    project.sourceCompatibility = 1.7
    project.targetCompatibility = 1.7

    // 编译参数
    [compileJava, compileTestJava]*.options*.compilerArgs = ["-Xlint:none", "-g"]
    [compileJava, javadoc, compileTestJava]*.options*.encoding = 'UTF-8'

    // 仓库
    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "http://repo.springsource.org/libs-snapshot" }
    }

    dependencies {
        // Logging
        compile "org.slf4j:slf4j-api:$slf4jVersion"
        runtime "org.slf4j:jcl-over-slf4j:$slf4jVersion"

        // Testing
        testCompile "junit:junit:$junitVersion"
        testCompile "org.hamcrest:hamcrest-library:$hamcrestVersion"
        testCompile "com.jayway.jsonpath:json-path:$jsonpathVersion"
        testCompile "org.mockito:mockito-core:$mockitoVersion"
        testCompile "org.springframework:spring-test:$springVersion"
        testRuntime "org.springframework:spring-context-support:$springVersion"
        testRuntime "ch.qos.logback:logback-classic:$logbackVersion"
    }
}

// 子项目共同的配置
// ----------------------
// 主要是javadoc的生成
configure(subprojects) { subproject ->
    apply from: "${gradleScriptDir}/maven.gradle"

    javadoc {
        options.memberLevel = org.gradle.external.javadoc.JavadocMemberLevel.PROTECTED
        options.author = true
        options.encoding = "UTF-8"
        options.header = subproject.name
        //options.overview = "${projectDir}/src/api/overview.html"
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = "sources"
        from sourceSets.main.allJava
    }
    task javadocJar(type: Jar) {
        classifier = "javadoc"
        from javadoc
    }

    artifacts {
        archives sourcesJar
        archives javadocJar
    }
}


project("avalon-service") {
	apply plugin: "maven"

	description = "Avalon core Service."

	configurations {
    	compile.extendsFrom providedCompile
	}

	dependencies {
		// Spring
	    compile "org.springframework:spring-aop:$springVersion"
	    compile "org.springframework:spring-core:$springVersion"
	    compile "org.springframework:spring-beans:$springVersion"
	    compile "org.springframework:spring-tx:$springVersion"
	    runtime "cglib:cglib-nodep:$cglibVersion"

	    // JODA
		compile("joda-time:joda-time:$jodaVersion", optional)

		// Testing
	    testCompile "org.hsqldb:hsqldb:$hsqldbVersion"
	    testCompile "org.hibernate:hibernate-entitymanager:$hibernateVersion"
	    testRuntime "org.hibernate:hibernate-validator:$hibernateValidatorVersion"
	    // JPA
    	testRuntime("org.hibernate.javax.persistence:hibernate-jpa-2.0-api:1.0.1.Final", optional)

    	testRuntime("org.springframework:spring-orm:$springVersion", optional)

	}
}

project("avalon-management") {
	apply plugin: "war"

  	description = "Avalon manage system site module."

  	dependencies {
  		compile project(":avalon-service")

  		// Spring mvc
	    compile "org.springframework:spring-web:$springVersion"
	    compile "org.springframework:spring-webmvc:$springVersion"

	    // Logging
    	runtime "ch.qos.logback:logback-classic:$logbackVersion"
  	}
}

project("avalon-portal"){
	apply plugin: "war"

  	description = "Avalon portal site module."

  	dependencies {
  		compile project(":avalon-service")

  		// Spring mvc
	    compile "org.springframework:spring-web:$springVersion"
	    compile "org.springframework:spring-webmvc:$springVersion"

	    compile("org.hibernate.javax.persistence:hibernate-jpa-2.0-api:1.0.1.Final", optional)
    	compile("org.springframework:spring-orm:$springVersion", optional)

    	// APIs
    	compile("javax.servlet:javax.servlet-api:3.0.1", provided)

    	// Jackson
	    compile "com.fasterxml.jackson.datatype:jackson-datatype-joda:$jacksonVersion"
	    compile "com.fasterxml.jackson.datatype:jackson-datatype-hibernate4:$jacksonVersion"
  	}
}

// 根项目配置
// ---------------------
// 主要是文档等的生成。
configure(rootProject) {


  	description = "Avalon Exporter"


  	// 根项目不生成jar
	configurations.archives.artifacts.clear()


	task api(type: Javadoc) {
	    group = "Documentation"
	    description = "Generates aggregated Javadoc API documentation."
	    title = "${rootProject.description} ${version} API"

	    options.memberLevel = org.gradle.external.javadoc.JavadocMemberLevel.PROTECTED
	    options.author = true
	    options.header = rootProject.description
	    options.overview = "src/api/overview.html"
	    options.splitIndex = true
	    options.encoding = "UTF-8"
	    options.linksOffline "http://docs.oracle.com/javase/6/docs/api/", "http://docs.oracle.com/javase/6/docs/api/"
	    options.linksOffline "http://static.springsource.org/spring/docs/3.1.x/javadoc-api/", "http://static.springsource.org/spring/docs/3.1.x/javadoc-api/"
	    options.linksOffline "http://static.springsource.org/spring-data/data-commons/docs/current/api/", "http://static.springsource.org/spring-data/data-commons/docs/current/api/"

	    source subprojects.collect { project ->
	      project.sourceSets.main.allJava
	    }

	    destinationDir = new File(buildDir, "api")
	    classpath = files(subprojects.collect { project ->
	      project.sourceSets.main.compileClasspath
	    })
	    maxMemory = "1024m"
	}

	task docsZip(type: Zip) {
	    group = "Distribution"
	    baseName = "avalon"
	    classifier = "docs"
	    description = "Builds -${classifier} archive containing api and reference."
	    from("src/dist") {
			include "changelog.txt"
			include "notice.txt"
	    }
	    from(api) {
			into "api"
	    }
	    from("src/reference") {
	    	into "reference"
	    }
	}

	artifacts {
		archives docsZip
	}

	dependencies {
		// For integration testing
		// testCompile project(":spring-data-rest-core")
		// testCompile project(":spring-data-rest-webmvc")
	}

	idea {
	    module {
	      downloadJavadoc = false
	      downloadSources = true
	    }
	    project {
			ipr {
		        withXml { xml ->
		          xml.node.component.find { it.@name == "VcsDirectoryMappings" }.mapping.@vcs = "Git"
		          xml.node.component.find { it.@name == "ProjectRootManager" }.output.@url = "file://\$PROJECT_DIR\$/build"
		        }
			}
    	}
	}
}

task wrapper(type:Wrapper){
	gradleVersion= '1.10'
}