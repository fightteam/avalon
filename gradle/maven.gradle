apply plugin: 'maven'

ext.optionalDeps = []
ext.providedDeps = []

ext.optional = { optionalDeps << it }
ext.provided = { providedDeps << it }

install {
  repositories.mavenInstaller {
    customizePom(pom, project)
  }
}

def customizePom(pom, gradleProject) {
  pom.whenConfigured { generatedPom ->
    // respect 'optional' and 'provided' dependencies
    gradleProject.optionalDeps.each { dep ->
      generatedPom.dependencies.find { it.artifactId == dep.name }?.optional = true
    }
    gradleProject.providedDeps.each { dep ->
      generatedPom.dependencies.find { it.artifactId == dep.name }?.scope = 'provided'
    }

    // eliminate test-scoped dependencies (no need in maven central poms)
    generatedPom.dependencies.removeAll { dep ->
      dep.scope == 'test'
    }

    // add all items necessary for maven central publication
    generatedPom.project {
      name = "AVALON"
      description = "The REST-full java web and html5 project."
      url = 'http://github.com/fightteam/avalon'
      organization {
        name = 'fightteam'
        url = 'http://github.com/fightteam'
      }
      licenses {
        license {
          name 'The Apache Software License, Version 2.0'
          url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          distribution 'repo'
        }
      }
      scm {
        url = 'http://github.com/fightteam/avalon'
        connection = 'scm:git:git://github.com/fightteam/avalon'
        developerConnection = 'scm:git:git://github.com/fightteam/avalon'
      }

      issueManagement {
         system = 'Github'
         url = 'https://github.com/fightteam/Avalon/issues'
      }

      developers {
        developer {
          id = 'excalibur'
          name = 'Excalibur Faith'
          email = 'lzy7750015@qq.com'
          url = 'http://excalibur.github.io/'
          roles {
            role = 'Project Leader'
          }
        }
      }
    }
  }
}